apply plugin: 'maven-publish'
apply plugin: 'signing'

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    if (project.plugins.findPlugin("com.android.library")) {
        from android.sourceSets.main.java.srcDirs
    } else {
        from sourceSets.main.java.srcDirs
    }
}

artifacts {
    archives androidSourcesJar
}

group = PUBLISH_GROUP_ID
version = PUBLISH_VERSION

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                groupId PUBLISH_GROUP_ID
                artifactId PUBLISH_ARTIFACT_ID
                version PUBLISH_VERSION
                if (project.plugins.findPlugin("com.android.library")) {
                    from components.release
                } else {
                    artifact("$buildDir/libs/${project.getName()}-${version}.jar")
                }

                artifact androidSourcesJar

                pom {
                    name = PUBLISH_ARTIFACT_ID
                    description = 'A small library containing a wrapper for the WebView of Android.'
                    url = 'https://github.com/bekannax/OnlineChatSdk-Android'
                    licenses {
                        license {
                            name = 'MIT'
                        }
                    }
                    developers {
                        developer {
                            id = 'bekannax'
                            name = 'Andrey Blinov'
                            email = 'bekannax@gmail.com'
                        }
                    }
                    scm {
                        connection = 'scm:git:github.com/bekannax/OnlineChatSdk-Android.git'
                        developerConnection = 'scm:git:ssh://github.com/bekannax/OnlineChatSdk-Android.git'
                        url = 'https://github.com/bekannax/OnlineChatSdk-Android'
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}
